class AnswerView {
  field int leftX, topY, rightX, bottomY, position;
  field String label;

  constructor AnswerView new(int positionIndex, String labelContent) {
    let label = labelContent;
    let position = positionIndex;
    do setCoordinates();

    return this;
  }

  method void draw() {
    do drawBorder();
    do printLabel();
    return;
  }

  method void drawBorder() {
    do BorderRect.draw(leftX, topY, rightX, bottomY);
    return;
  }

  method void printLabel() {
    var int row, column;
    let row = (topY / 11) + 1;
    let column = (leftX / 8) + 2;

    do Output.moveCursor(row, column);
    do Output.printInt(position);
    do Output.printString(". ");
    do Output.printString(label);

    return;
  }

  method void dispose() {
    do label.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method void setCoordinates() {
    if ((position = 0) | (position = 2)) {
      let leftX = 11;
      let rightX = 252;
    }

    if ((position = 1) | (position = 3)) {
      let leftX = 260;
      let rightX = 501;
    }

    if ((position = 0) | (position = 1)) {
      let topY = 165;
      let bottomY = 195;
    }

    if ((position = 2) | (position = 3)) {
      let topY = 200;
      let bottomY = 230;
    }
    return;
  }
}
