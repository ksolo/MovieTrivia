class QuestionView {

  field String label;

  constructor QuestionView new(String labelContent) {
    let label = labelContent;
    return this;
  }

  method void draw() {
    do createBoarder();
    do displayLabel();
    return;
  }

  method void createBoarder() {
    do Screen.setColor(true);
    do Screen.drawLine(11, 11, 501, 11);
    do Screen.drawLine(11, 11, 11, 147);
    do Screen.drawLine(501, 11, 501, 147);
    do Screen.drawLine(11, 147, 501, 147);
    return;
  }

  method void displayLabel() {
    do Output.moveCursor(labelYCoordinate(), labelXCoordinate());
    do Output.printString(label);
    return;
  }

  method int labelXCoordinate() {
    var int lineLength, midPoint, centerOffset;

    let lineLength = 64; // number of characters in a line
    let midPoint = lineLength / 2;
    let centerOffset = label.length() / 2;

    return (midPoint - centerOffset);
  }

  method int labelYCoordinate() {
    var int rowsWithinBorder, verticalCenter, rowOffset;

    let rowsWithinBorder = 11; // ((147 - 2 * rowHeight) / rowHeight) = 11
    let rowOffset = 1;
    let verticalCenter = rowsWithinBorder / 2;

    return rowOffset + verticalCenter;
  }

  method void dispose() {
    do label.dispose();
    do Memory.deAlloc(this);
    return;
  }
}
